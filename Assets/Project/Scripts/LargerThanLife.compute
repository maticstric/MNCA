#pragma kernel Init
#pragma kernel Main

#define GROUP_SIZE_X 8
#define GROUP_SIZE_Y 8

RWTexture2D<float4> CurrTexture;
RWTexture2D<float4> PrevTexture;

int Range;
int B1;
int B2;
int A1;
int A2;

float Density;

float rand(float2 uv) {
    float2 K1 = float2(
        23.14069263277926, // e^pi (Gelfond's constant)
        2.665144142690225  // 2^sqrt(2) (Gelfond-Schneider constant)
    );

    return frac(cos(dot(uv, K1)) * 12345.6789);
}

[numthreads(GROUP_SIZE_X,GROUP_SIZE_Y,1)]
void Init (uint3 id : SV_DispatchThreadID) {
    float random = rand(id.xy);
    CurrTexture[id.xy] = rand(id.xy) > Density ? float4(1, 1, 1, 1) : float4(0, 0, 0, 0);
}

[numthreads(GROUP_SIZE_X,GROUP_SIZE_Y,1)]
void Main (uint3 id : SV_DispatchThreadID) {
    float4 sumFloat = 0;

    for (int i = -Range; i <= Range; i++) {
        for (int j = -Range; j <= Range; j++) {
            sumFloat += PrevTexture[id.xy + uint2(i, j)];
        }
    }

    int prevState = round(PrevTexture[id.xy].w);
    int sum = round(sumFloat.w);

    int newState = 0;

    if (prevState == 0) {
        if (sum >= B1 && sum <= B2) {
            newState = 1;
        }
    } else {
        if (sum >= A1 && sum <= A2) {
            newState = 1;
        }
    }

    CurrTexture[id.xy] = newState;
}